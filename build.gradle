plugins {
    id 'java'
    alias(libs.plugins.nexus.publish)
}

apply from: "${rootDir}/gradle/publish-root.gradle"

allprojects {
    group = 'com.getyourguide.openapi.validation'
    description = 'OpenAPI Validation library'
    version = '3.0.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(libs.versions.java.get())
        }
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    if(it.parent.name == 'examples') {
        apply plugin: 'java'
    } else {
        apply plugin: 'java-library'
        apply plugin: 'jacoco'

        dependencies {
            // Testing
            testImplementation(libs.junit.jupiter.api)
            testRuntimeOnly(libs.junit.jupiter.engine)
            testImplementation(libs.mockito.core)
            testImplementation(libs.mockito.junit.jupiter)
        }

        jacoco {
            toolVersion = libs.versions.jacoco.get()
        }

        jacocoTestReport {
            reports {
                xml.required = true
                csv.required = false
                html.required = true
            }
        }

        test {
            useJUnitPlatform()
        }
    }

    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    dependencies {
        // Lombok annotations to reduce boilerplate code
        compileOnly(libs.lombok)
        annotationProcessor(libs.lombok)
        testCompileOnly(libs.lombok)
        testAnnotationProcessor(libs.lombok)
    }

    checkstyle {
        toolVersion = libs.versions.checkstyle.get()
        configDirectory.set(file("$rootProject.projectDir/config"))
        checkstyleMain.source = "src/main/java"
        checkstyleMain.exclude('**/build/generated/**')
        checkstyleTest.source = "src/main/java"
        checkstyleTest.exclude('**/build/generated/**')
    }

    pmd {
        toolVersion = libs.versions.pmd.get()
        consoleOutput = true
        ruleSets = ["$rootDir/ruleset.xml"]
    }

    // Needed for security. See:
    // - https://github.com/getyourguide/openapi-validation-java/security/dependabot/7
    // - https://github.com/getyourguide/openapi-validation-java/security/dependabot/6
    // Hopefully with spring-boot 3.4.2+ this won't be needed anymore and can be removed.
    configurations.configureEach {
        resolutionStrategy.eachDependency {details ->
            if (details.requested.group == 'ch.qos.logback'
                && (details.requested.name == 'logback-core' || details.requested.name == 'logback-classic')
            ) {
                def parse = { String v -> v.tokenize('.').collect { it.padLeft(3, '0') }.join() }
                def current = parse(details.requested.version)
                def minimum = parse('1.5.15')
                if (current < minimum) {
                    details.useVersion '1.5.15'
                }
            }
        }
    }
}
