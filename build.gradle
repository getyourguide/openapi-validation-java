plugins {
    id 'java'
    alias(libs.plugins.nexus.publish)
}

def javaVersion = providers.environmentVariable("JAVA_BUILD_VERSION").getOrElse("17")

apply from: "${rootDir}/gradle/publish-root.gradle"

allprojects {
    group = 'com.getyourguide.openapi.validation'
    description = 'OpenAPI Validation library'
    version = '1.2.7'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    if(it.parent.name == 'examples' || it.parent.name == 'test') {
        apply plugin: 'java'
    } else {
        apply plugin: 'java-library'
        apply plugin: 'jacoco'

        dependencies {
            // Testing
            testImplementation(libs.junit.jupiter.api)
            testRuntimeOnly(libs.junit.jupiter.engine)
            testImplementation(libs.mockito.core)
            testImplementation(libs.mockito.junit.jupiter)
        }

        jacoco {
            toolVersion = libs.versions.jacoco.get()
        }

        jacocoTestReport {
            reports {
                xml.required = true
                csv.required = false
                html.required = true
            }
        }

        test {
            useJUnitPlatform()
        }
    }

    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    dependencies {
        // Lombok annotations to reduce boilerplate code
        compileOnly(libs.lombok)
        annotationProcessor(libs.lombok)
        testCompileOnly(libs.lombok)
        testAnnotationProcessor(libs.lombok)
    }

    checkstyle {
        toolVersion = libs.versions.checkstyle.get()
        configDirectory.set(file("$rootProject.projectDir/config"))
        checkstyleMain.source = "src/main/java"
        checkstyleMain.exclude('**/build/generated/**')
        checkstyleTest.source = "src/main/java"
        checkstyleTest.exclude('**/build/generated/**')
    }

    pmd {
        toolVersion = libs.versions.pmd.get()
        consoleOutput = true
        ruleSets = ["$rootDir/ruleset.xml"]
    }
}
