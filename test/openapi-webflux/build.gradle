
plugins {
    id 'java-library'
    id 'java-test-fixtures'
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.openapi.generator)
}

dependencies {
    testFixturesImplementation platform("com.getyourguide.java.bom:gyg-spring-lib-bom:1.1.0")

    testFixturesCompileOnly project(':openapi-validation-api')
    testFixturesCompileOnly project(':openapi-validation-core')

    testFixturesCompileOnly 'org.springframework.boot:spring-boot-starter'
    testFixturesCompileOnly 'org.springframework:spring-webflux'

    // TODO use spotbugs instead and also apply to all modules?
    testFixturesImplementation(libs.find.bugs)

    testFixturesApi platform("com.getyourguide.java.bom:gyg-spring-lib-bom:1.1.0")
    testFixturesApi 'org.springframework.boot:spring-boot-starter-test'
    // For openapi generated code
    testFixturesApi 'org.springframework.boot:spring-boot-starter-validation'
    testFixturesApi(libs.openapi.tools.jacksonDatabindNullable)
    testFixturesApi(libs.jakarta.validation.api)
    testFixturesApi(libs.swagger.annotations)
    testFixturesApi 'org.apache.tomcat.embed:tomcat-embed-core' // For jakarta.servlet.ServletContext
}


def generatedSourceDirectory = "$buildDir/generated/openapi"
sourceSets {
    testFixtures {
        java {
            srcDir generatedSourceDirectory + '/src/main/java'
        }
    }
}

openApiValidate {
    inputSpec = "$projectDir/../openapi.yaml"
    recommend = true
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/../openapi.yaml"
    outputDir = generatedSourceDirectory
    apiPackage = "com.getyourguide.openapi.validation.test.openapi.webflux"
    invokerPackage = "com.getyourguide.openapi.validation.test.openapi.webflux"
    modelPackage = "com.getyourguide.openapi.validation.test.openapi.webflux.model"
    configOptions = [
            useSpringBoot3         : "true",
            reactive               : "true",
            dateLibrary            : "java8",
            performBeanValidation  : "true",
            hideGenerationTimestamp: "true",
            serializableModel      : "true",
            interfaceOnly          : "true",
            skipDefaultInterface   : "true",
            useTags                : "true"
    ]
}

tasks.openApiGenerate.dependsOn tasks.openApiValidate
tasks.compileJava.dependsOn tasks.openApiGenerate
