import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.openapi.generator)
}

dependencies {
    testFixturesImplementation platform(SpringBootPlugin.BOM_COORDINATES)

    testFixturesCompileOnly project(':openapi-validation-api')
    testFixturesCompileOnly project(':openapi-validation-core')

    testFixturesCompileOnly 'org.springframework.boot:spring-boot-starter'
    testFixturesCompileOnly 'org.springframework:spring-web'

    // TODO use spotbugs instead and also apply to all modules?
    testFixturesImplementation(libs.find.bugs)

    testFixturesApi platform(SpringBootPlugin.BOM_COORDINATES)
    testFixturesApi 'org.springframework.boot:spring-boot-starter-test'
    testFixturesApi 'org.springframework.boot:spring-boot-webmvc-test'
    // For openapi generated code
    testFixturesApi 'org.springframework.boot:spring-boot-starter-validation'
    testFixturesApi(libs.openapi.tools.jacksonDatabindNullable)
    testFixturesApi(libs.jakarta.validation.api)
    testFixturesApi(libs.swagger.annotations)
    testFixturesApi 'org.apache.tomcat.embed:tomcat-embed-core' // For jakarta.servlet.ServletContext
}


def generatedSourceDirectory = "$buildDir/generated/openapi"
sourceSets {
    testFixtures {
        java {
            srcDir generatedSourceDirectory + '/src/main/java'
        }
    }
}

openApiValidate {
    inputSpec = "$projectDir/../openapi.yaml"
    recommend = true
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/../openapi.yaml"
    outputDir = generatedSourceDirectory
    apiPackage = "com.getyourguide.openapi.validation.test.openapi.web"
    invokerPackage = "com.getyourguide.openapi.validation.test.openapi.web"
    modelPackage = "com.getyourguide.openapi.validation.test.openapi.web.model"
    configOptions = [
            useSpringBoot3         : "true",
            dateLibrary            : "java8",
            performBeanValidation  : "true",
            hideGenerationTimestamp: "true",
            serializableModel      : "true",
            interfaceOnly          : "true",
            skipDefaultInterface   : "true",
            useTags                : "true"
    ]
}

tasks.openApiGenerate.dependsOn tasks.openApiValidate
tasks.compileJava.dependsOn tasks.openApiGenerate
