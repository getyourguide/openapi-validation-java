import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.openapi.generator)
}

apply from: "${rootDir}/gradle/publish-module.gradle"

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)

    compileOnly project(':openapi-validation-api')
    compileOnly project(':openapi-validation-core')

    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.springframework:spring-web'

    // TODO use spotbugs instead and also apply to all modules?
    implementation(libs.find.bugs)

    api 'org.springframework.boot:spring-boot-starter-test'

    // For openapi generated code
    implementation(libs.openapi.tools.jacksonDatabindNullable)
    implementation(libs.swagger.annotations)
    implementation(libs.jakarta.validation.api)
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core' // For jakarta.servlet.ServletContext
}


def generatedSourceDirectory = "$buildDir/generated/openapi"
sourceSets {
    main {
        java {
            srcDir generatedSourceDirectory + '/src/main/java'
        }
    }
}

openApiValidate {
    inputSpec = "$projectDir/src/main/resources/openapi.yaml"
    recommend = true
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/openapi.yaml"
    outputDir = generatedSourceDirectory
    apiPackage = "com.getyourguide.openapi.validation.test.openapi"
    invokerPackage = "com.getyourguide.openapi.validation.test.openapi"
    modelPackage = "com.getyourguide.openapi.validation.test.openapi.model"
    configOptions = [
            useSpringBoot3         : "true",
            dateLibrary            : "java8",
            performBeanValidation  : "true",
            hideGenerationTimestamp: "true",
            serializableModel      : "true",
            interfaceOnly          : "true",
            skipDefaultInterface   : "true",
            useTags                : "true"
    ]
}

tasks.openApiGenerate.dependsOn tasks.openApiValidate
tasks.compileJava.dependsOn tasks.openApiGenerate
